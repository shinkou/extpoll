#!/usr/bin/env perl

use strict;
use warnings;
use File::Find;
use Getopt::Long;
use Pod::Usage;

my ($extensions, $help);

sub start
{
	my @dirs = @_;
	my $h = {};
	my %exts = ();
	if ($extensions)
	{
		%exts = (map{$_ => 1} split(/\s*,\s*/, $extensions));
	}

	find
	(
		sub
		{
			my $fn = $_;

			if ($fn =~ /([^.]+)$/)
			{
				my $ext = $1;
				# exclude filenames without extension
				if ($ext ne $fn and ".${ext}" ne $fn)
				{
					if (! %exts or $exts{$ext})
					{
						$h->{lc($ext)} ++;
					}
				}
			}
		}
		, @dirs
	);

	my @ks = sort(keys(%{$h}));
	print("Frequency  Extension\n");
	for my $k (@ks)
	{
		printf("%9d  %s\n", $h->{$k}, $k);
	}
}

GetOptions
(
	'e|extensions=s' => \$extensions
	, 'h|help|?' => \$help
) or pod2usage(2);

pod2usage(1) if ($help);

(scalar @ARGV) ? start(@ARGV) : start('.');

__END__

=head1 NAME

extpoll - Count file extensions

=head1 SYNOPSIS

extpoll [options] [directory ...]

=head1 OPTIONS

=over

=item B<-e, --extensions> EXTENSIONS

A list of extensions separated by commas

=item B<-?, -h, --help>

Print this message.

=back

=head1 DESCRIPTION

This program search, groups, and counts files according to their extensions.

=head1 AUTHOR

Chun-Kwong Wong (http://github.com/shinkou/)

=head1 COPYRIGHT AND LICENSE

This software is released under the MIT license.

=cut
